defmodule PortalWeb.Live.Policies.ShowTest do
  use PortalWeb.ConnCase, async: true

  setup do
    account = Fixtures.Accounts.create_account()
    actor = Fixtures.Actors.create_actor(type: :account_admin_user, account: account)
    identity = Fixtures.Auth.create_identity(account: account, actor: actor)
    subject = Fixtures.Auth.create_subject(account: account, actor: actor, identity: identity)

    resource = Fixtures.Resources.create_resource(account: account)

    policy =
      Fixtures.Policies.create_policy(
        account: account,
        subject: subject,
        resource: resource,
        conditions: [
          %{
            property: :current_utc_datetime,
            operator: :is_in_day_of_week_time_ranges,
            values: [
              "M/true/UTC",
              "T//UTC",
              "W/true/UTC",
              "R//UTC",
              "F//UTC",
              "S/10:00:00-15:00:00/UTC",
              "U/23:00:00-23:59:59/UTC"
            ]
          },
          %{
            property: :provider_id,
            operator: :is_in,
            values: [identity.provider_id]
          },
          %{
            property: :remote_ip,
            operator: :is_not_in_cidr,
            values: ["0.0.0.0/0"]
          },
          %{
            property: :remote_ip_location_region,
            operator: :is_in,
            values: ["US"]
          }
        ],
        description: "Test Policy"
      )

    policy = Repo.preload(policy, [:group, :resource])

    %{
      account: account,
      actor: actor,
      identity: identity,
      subject: subject,
      resource: resource,
      policy: policy
    }
  end

  test "redirects to sign in page for unauthorized user", %{
    account: account,
    policy: policy,
    conn: conn
  } do
    path = ~p"/#{account}/policies/#{policy}"

    assert live(conn, path) ==
             {:error,
              {:redirect,
               %{
                 to: ~p"/#{account}?#{%{redirect_to: path}}",
                 flash: %{"error" => "You must sign in to access this page."}
               }}}
  end

  test "raises NotFoundError for deleted policy", %{
    account: account,
    policy: policy,
    identity: identity,
    conn: conn
  } do
    {:ok, deleted_policy} = Fixtures.Policies.delete_policy(policy)

    assert_raise PortalWeb.LiveErrors.NotFoundError, fn ->
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{deleted_policy}")
    end
  end

  test "renders breadcrumbs item", %{
    account: account,
    policy: policy,
    identity: identity,
    conn: conn
  } do
    {:ok, _lv, html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    assert item = html |> Floki.parse_fragment!() |> Floki.find("[aria-label='Breadcrumb']")
    breadcrumbs = String.trim(Floki.text(item))
    assert breadcrumbs =~ "Policies"
    assert breadcrumbs =~ policy.group.name
    assert breadcrumbs =~ policy.resource.name
  end

  test "allows editing policy", %{
    account: account,
    policy: policy,
    identity: identity,
    conn: conn
  } do
    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    assert lv
           |> element("a", "Edit Policy")
           |> render_click() ==
             {:error,
              {:live_redirect, %{to: ~p"/#{account}/policies/#{policy}/edit", kind: :push}}}
  end

  test "renders policy details", %{
    account: account,
    actor: actor,
    identity: identity,
    policy: policy,
    conn: conn
  } do
    policy =
      policy
      |> Portal.Repo.preload(:group)
      |> Portal.Repo.preload(:resource)

    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    table =
      lv
      |> element("#policy")
      |> render()
      |> vertical_table_to_map()

    assert table["group"] =~ policy.group.name
    assert table["resource"] =~ policy.resource.name
    assert table["description"] =~ policy.description
    assert table["created"] =~ actor.name

    assert table["conditions"] =~ "This policy can be used on"
    assert table["conditions"] =~ "Mondays"
    assert table["conditions"] =~ "Wednesdays"
    assert table["conditions"] =~ "Saturdays (10:00:00 - 15:00:00 UTC)"
    assert table["conditions"] =~ "Sundays (23:00:00 - 23:59:59 UTC)"
    assert table["conditions"] =~ "from United States of America"
    assert table["conditions"] =~ "from IP addresses that are"
    assert table["conditions"] =~ "not in"
    assert table["conditions"] =~ "0.0.0.0"
    assert table["conditions"] =~ "when signed in"
    assert table["conditions"] =~ "with"
    assert table["conditions"] =~ "provider(s)"
  end

  test "renders policy details when created by API", %{
    account: account,
    identity: identity,
    resource: resource,
    conn: conn
  } do
    actor = Fixtures.Actors.create_actor(type: :api_client, account: account)
    subject = Fixtures.Auth.create_subject(account: account, actor: actor)

    policy =
      Fixtures.Policies.create_policy(account: account, subject: subject, resource: resource)
      |> Portal.Repo.preload(:group)
      |> Portal.Repo.preload(:resource)

    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    table =
      lv
      |> element("#policy")
      |> render()
      |> vertical_table_to_map()

    assert table["group"] =~ policy.group.name
    assert table["resource"] =~ policy.resource.name
    assert table["description"] =~ policy.description
    assert table["created"] =~ actor.name
  end

  test "renders logs table", %{
    account: account,
    identity: identity,
    resource: resource,
    policy: policy,
    conn: conn
  } do
    policy_authorization =
      Fixtures.PolicyAuthorizations.create_policy_authorization(
        account: account,
        resource: resource,
        policy: policy
      )

    policy_authorization = Repo.preload(policy_authorization, client: [:actor], gateway: [:site])

    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    [row] =
      lv
      |> element("#policy_authorizations")
      |> render()
      |> table_to_map()

    assert row["authorized"]
    assert row["client, actor"] =~ policy_authorization.client.name
    assert row["client, actor"] =~ "owned by #{policy_authorization.client.actor.name}"
    assert row["client, actor"] =~ to_string(policy_authorization.client_remote_ip)

    assert row["gateway"] =~
             "#{policy_authorization.gateway.site.name}-#{policy_authorization.gateway.name} #{policy_authorization.gateway.last_seen_remote_ip}"
  end

  test "allows deleting policy", %{
    account: account,
    policy: policy,
    identity: identity,
    conn: conn
  } do
    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    assert lv
           |> element("button[type=submit]", "Delete Policy")
           |> render_click() ==
             {:error, {:live_redirect, %{to: ~p"/#{account}/policies", kind: :push}}}

    refute Repo.get(Portal.Policy, policy.id)
  end

  test "allows disabling and enabling policy", %{
    account: account,
    policy: policy,
    identity: identity,
    conn: conn
  } do
    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/policies/#{policy}")

    assert lv
           |> element("button[type=submit]", "Disable")
           |> render_click() =~ "(disabled)"

    assert Repo.get(Portal.Policy, policy.id).disabled_at

    refute lv
           |> element("button[type=submit]", "Enable")
           |> render_click() =~ "(disabled)"

    refute Repo.get(Portal.Policy, policy.id).disabled_at
  end
end
