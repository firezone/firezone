defmodule PortalWeb.Live.Sites.Gateways.IndexTest do
  use PortalWeb.ConnCase, async: true

  import Portal.AccountFixtures
  import Portal.ActorFixtures
  import Portal.GatewayFixtures
  import Portal.IdentityFixtures
  import Portal.SiteFixtures
  import Portal.TokenFixtures

  setup do
    account = account_fixture()
    actor = admin_actor_fixture(account: account)
    identity = identity_fixture(account: account, actor: actor)
    site = site_fixture(account: account)

    %{
      account: account,
      identity: identity,
      site: site
    }
  end

  test "redirects to sign in page for unauthorized user", %{
    account: account,
    site: site,
    conn: conn
  } do
    path = ~p"/#{account}/sites/#{site}/gateways"

    assert live(conn, path) ==
             {:error,
              {:redirect,
               %{
                 to: ~p"/#{account}?#{%{redirect_to: path}}",
                 flash: %{"error" => "You must sign in to access this page."}
               }}}
  end

  test "renders breadcrumbs item", %{
    account: account,
    identity: identity,
    site: site,
    conn: conn
  } do
    {:ok, _lv, html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/sites/#{site}/gateways")

    assert item = html |> Floki.parse_fragment!() |> Floki.find("[aria-label='Breadcrumb']")
    breadcrumbs = String.trim(Floki.text(item))
    assert breadcrumbs =~ "Sites"
  end

  test "renders gateways table", %{
    account: account,
    identity: identity,
    site: site,
    conn: conn
  } do
    gateway =
      gateway_fixture(
        account: account,
        site: site,
        context: %{user_agent: "iOS/12.5 (iPhone) connlib/1.3.2"}
      )

    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/sites/#{site}/gateways")

    [row] =
      lv
      |> element("#gateways")
      |> render()
      |> table_to_map()

    assert row == %{
             "instance" => gateway.name,
             "remote ip" => to_string(gateway.last_seen_remote_ip),
             "status" => "Offline",
             "version" => "1.3.2"
           }
  end

  test "updates gateways table using presence events", %{
    account: account,
    identity: identity,
    site: site,
    conn: conn
  } do
    token = site_token_fixture(account: account, site: site)

    gateway =
      gateway_fixture(
        account: account,
        site: site,
        context: %{user_agent: "iOS/12.5 (iPhone) connlib/1.3.2"}
      )

    {:ok, lv, _html} =
      conn
      |> authorize_conn(identity)
      |> live(~p"/#{account}/sites/#{site}/gateways")

    :ok = Portal.Presence.Gateways.Site.subscribe(site.id)
    :ok = Portal.Presence.Gateways.connect(gateway, token.id)
    expected_topic = "presences:sites:#{site.id}"
    assert_receive %Phoenix.Socket.Broadcast{topic: ^expected_topic}

    wait_for(fn ->
      [row] =
        lv
        |> element("#gateways")
        |> render()
        |> table_to_map()

      assert row == %{
               "instance" => gateway.name,
               "remote ip" => to_string(gateway.last_seen_remote_ip),
               "status" => "Online",
               "version" => "1.3.2"
             }
    end)
  end
end
