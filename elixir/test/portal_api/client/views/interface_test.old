defmodule PortalAPI.Client.Views.InterfaceTest do
  use PortalAPI.ChannelCase, async: true
  alias PortalAPI.Client.Views.Interface

  describe "render/1" do
    test "renders interface config with Do53 resolvers" do
      account =
        Fixtures.Accounts.create_account(
          config: %{
            clients_upstream_dns: %{
              type: :custom,
              addresses: [
                %{address: "1.1.1.1"},
                %{address: "8.8.8.8"}
              ]
            },
            search_domain: "example.com"
          }
        )

      client = Fixtures.Clients.create_client(account: account) |> Portal.Repo.preload(:account)

      result = Interface.render(client)

      assert result.search_domain == "example.com"
      assert result.ipv4 == client.ipv4
      assert result.ipv6 == client.ipv6

      # New Do53 format
      assert result.upstream_do53 == [
               %{ip: "1.1.1.1"},
               %{ip: "8.8.8.8"}
             ]

      # Legacy format
      assert result.upstream_dns == [
               %{protocol: :ip_port, address: "1.1.1.1:53"},
               %{protocol: :ip_port, address: "8.8.8.8:53"}
             ]

      # No DoH provider set
      assert result.upstream_doh == []
    end

    test "renders interface config with DoH provider despite addresses if type is :secure" do
      account =
        Fixtures.Accounts.create_account(
          config: %{
            clients_upstream_dns: %{
              type: :secure,
              doh_provider: :opendns,
              addresses: [
                %{address: "1.1.1.1"}
              ]
            }
          }
        )

      client = Fixtures.Clients.create_client(account: account) |> Portal.Repo.preload(:account)

      result = Interface.render(client)

      assert result.upstream_doh == [
               %{url: "https://doh.opendns.com/dns-query"}
             ]

      assert result.upstream_do53 == []
      assert result.upstream_dns == []
    end
  end
end
